&НаКлиенте
Процедура Вперед(Команда)
	Дата = НачалоДня(Дата + 86400);
    ОбновитьДашборд();
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	Дата = НачалоДня(Дата - 86400);
    ОбновитьДашборд(); 
КонецПроцедуры
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Прием" Тогда
		ОбновитьДашборд();
	КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура ДашбордПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	
	Для каждого Элемент из ЗначенияИзмерений Цикл
		ЗначенияЗаполнения.Вставить("Врач", Элемент.Значение);
	КонецЦикла;
	
	ЗначенияЗаполнения.Вставить("ДатаВремяНачала", Начало);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ЗаписьНаПрием.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДашбордПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)
	Если НЕ (ТекущийЭлемент = "Назад") ИЛИ НЕ (ТекущийЭлемент = "Вперед") Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДашборд();
КонецПроцедуры

&НаКлиенте
Процедура ДашбордПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДашбордПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДашбордВыбор(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ВыделенныеЭлементы = Элемент.ВыделенныеЭлементы;
	
	Если Не ЗначениеЗаполнено(ВыделенныеЭлементы) Тогда
		Возврат;
	КонецЕсли;
	Ссылка = ВыделенныеЭлементы[0].Значение;
	
	ОткрытьЗначениеАсинх(Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ТекущаяДатаСеанса();
    ОбновитьДашборд();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ОбновитьДашборд();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДашборд()
	Дашборд.ТекущиеПериодыОтображения.Очистить();
	НачалоОтображения = НачалоДня(Дата) + 9 * 60 * 60; // 9:00
	КонецОтображения = НачалоДня(Дата) + 21 * 60 * 60 + 10 * 60; // 21:00
	Дашборд.ТекущиеПериодыОтображения.Добавить(НачалоОтображения, КонецОтображения);
	
	Дашборд.ВыравниватьГраницыЭлементовПоШкалеВремени = Ложь;
	
	ИзмеренияДашборда = Дашборд.Измерения;
	ИзмеренияДашборда.Очистить();
	
	ИзмерениеВрачи = ИзмеренияДашборда.Добавить("Врачи");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Наименование КАК Наименование,
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.Родитель.Наименование КАК РодительНаименование
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Родитель = &Группа";
	
	Запрос.УстановитьПараметр("Группа", Справочники.Сотрудники.НайтиПоНаименованию("Врачи"));
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СоответствиеЦветов = Новый Соответствие;
	ДоступныеЦвета = Новый Массив;
	ДоступныеЦвета.Добавить(Новый Цвет(255, 200, 200)); // Светло-красный
	ДоступныеЦвета.Добавить(Новый Цвет(200, 255, 200)); // Светло-зеленый
	ДоступныеЦвета.Добавить(Новый Цвет(200, 200, 255)); // Светло-синий
	ДоступныеЦвета.Добавить(Новый Цвет(255, 255, 200)); // Светло-желтый
	ДоступныеЦвета.Добавить(Новый Цвет(255, 200, 255)); // Светло-пурпурный
	ДоступныеЦвета.Добавить(Новый Цвет(200, 255, 255)); // Светло-голубой
	ДоступныеЦвета.Добавить(Новый Цвет(230, 230, 230)); // Светло-серый
	ДоступныеЦвета.Добавить(Новый Цвет(255, 220, 200)); // Светло-оранжевый
	
	ИндексЦвета = 0;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НовыйВрач = ИзмерениеВрачи.Элементы.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		НовыйВрач.Текст = ВыборкаДетальныеЗаписи.Наименование;
		Если ИндексЦвета >= ДоступныеЦвета.Количество() Тогда
			ИндексЦвета = 0; // Начинаем сначала, если цветов не хватило
		КонецЕсли;
		
		СоответствиеЦветов.Вставить(ВыборкаДетальныеЗаписи.Ссылка, ДоступныеЦвета[ИндексЦвета]);
		ИндексЦвета = ИндексЦвета + 1;
	КонецЦикла;
	
	ЭлементыДашборда = Дашборд.Элементы;
	ЭлементыДашборда.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасписаниеВрачей.ДатаВремяНачала КАК ДатаВремяНачала,
		|	РасписаниеВрачей.ДатаВремяОкончания КАК ДатаВремяОкончания,
		|	РасписаниеВрачей.Врач КАК Врач,
		|	РасписаниеВрачей.Услуга.Наименование КАК УслугаНаименование,
		|	РасписаниеВрачей.Питомец.Наименование КАК ПитомецНаименование,
		|	РасписаниеВрачей.Запись.Ссылка КАК ЗаписьСсылка,
		|	РасписаниеВрачей.Питомец.Хозяин.Наименование КАК ПитомецХозяинНаименование,
		|	РасписаниеВрачей.Питомец.Хозяин.Телефон КАК ПитомецХозяинТелефон
		|ИЗ
		|	РегистрСведений.РасписаниеВрачей КАК РасписаниеВрачей
		|ГДЕ
		|	РасписаниеВрачей.ДатаВремяНачала >= &НачалоДня
		|	И РасписаниеВрачей.ДатаВремяОкончания <= &КонецДня";
	
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(Дата));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СоответствиеЗначений = Новый Соответствие;
		СоответствиеЗначений.Вставить("Врачи", ВыборкаДетальныеЗаписи.Врач);
		
		НовыйЭлемент = ЭлементыДашборда.Добавить(
			ВыборкаДетальныеЗаписи.ДатаВремяНачала, 
			ВыборкаДетальныеЗаписи.ДатаВремяОкончания
		);
		НовыйЭлемент.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
		
		ТекстЭлемента = "";
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ПитомецХозяинНаименование) Тогда
			ТекстЭлемента = ТекстЭлемента + ВыборкаДетальныеЗаписи.ПитомецХозяинНаименование;
		КонецЕсли;
		
		ТекстЭлемента = ТекстЭлемента + " Тлф: ";
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ПитомецХозяинТелефон) Тогда
			ТекстЭлемента = ТекстЭлемента + ВыборкаДетальныеЗаписи.ПитомецХозяинТелефон;
		КонецЕсли;
		
		ТекстЭлемента = ТекстЭлемента + " Услуга: ";
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.УслугаНаименование) Тогда
			ТекстЭлемента = ТекстЭлемента + ВыборкаДетальныеЗаписи.УслугаНаименование;
		КонецЕсли;
		
		НовыйЭлемент.Текст = ТекстЭлемента;
		
		Если СоответствиеЦветов[ВыборкаДетальныеЗаписи.Врач] <> Неопределено Тогда
			НовыйЭлемент.ЦветФона = СоответствиеЦветов[ВыборкаДетальныеЗаписи.Врач];
		КонецЕсли;
		
		НовыйЭлемент.Значение = ВыборкаДетальныеЗаписи.ЗаписьСсылка;
		
	КонецЦикла;
КонецПроцедуры
