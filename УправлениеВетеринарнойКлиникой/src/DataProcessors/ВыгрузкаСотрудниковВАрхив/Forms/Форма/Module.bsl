
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Декорация1.Видимость = Ложь;
	
КонецПроцедуры
&НаКлиенте
Процедура Выгрузить(Команда)
	
	ОписаниеФоновогоЗадания = НачатьФормированиеАрхива(УникальныйИдентификатор);
	
	ИдентификаторЗадания = ОписаниеФоновогоЗадания.УникальныйИдентификатор;
	АдресРезультата = ОписаниеФоновогоЗадания.АдресРезультата;
	
	ПодключитьОбработчикОжидания("ПроверитьРезультатФоновогоЗадания", 1, Истина);
	
	Элементы.Декорация1.Видимость = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРезультатФоновогоЗадания()
	
	СтатусЗадания = СтатусФоновогоЗадания(ИдентификаторЗадания);
	
	Если СтатусЗадания = "Ошибка" Тогда
		Сообщить("Ошибка");
		Элементы.Декорация1.Видимость = Ложь;
	ИначеЕсли СтатусЗадания = "Активно" Тогда
		ПодключитьОбработчикОжидания("ПроверитьРезультатФоновогоЗадания", 1, Истина);
	ИначеЕсли СтатусЗадания = "Завершено" Тогда
		
		ДанныеАрхива = ПолучитьИзВременногоХранилища(АдресРезультата);
		
		// Сохраняем временно на сервере и отдаем клиенту
		Если ДанныеАрхива <> Неопределено Тогда
			ВременныйФайл = ПоместитьВоВременноеХранилище(ДанныеАрхива, Новый УникальныйИдентификатор());
			ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
			ПолучитьФайлССервераАсинх(ВременныйФайл, "export.zip", ПараметрыДиалога);
		КонецЕсли;
		
		Элементы.Декорация1.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтатусФоновогоЗадания(ИдентификаторЗадания)
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат "Ошибка";
	КонецЕсли;
	
	Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
		Возврат "Активно";
	КонецЕсли;
		
	Если ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно
		Или ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Отменено Тогда
		Возврат "Ошибка";
	КонецЕсли;
		
	Возврат "Завершено";	
	
КонецФункции

&НаСервере
Функция НачатьФормированиеАрхива(УникальныйИдентификатор) 

	АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПараметрыФоновогоЗадания = Новый Массив;
	ПараметрыФоновогоЗадания.Добавить(АдресРезультата);
	
	ФоновоеЗадание = ФоновыеЗадания.Выполнить("ФормированиеАрхивов.СформироватьАрхивВФоне", ПараметрыФоновогоЗадания);

	Результат = Новый Структура;
	Результат.Вставить("УникальныйИдентификатор", ФоновоеЗадание.УникальныйИдентификатор);
	Результат.Вставить("АдресРезультата", АдресРезультата);

	Возврат Результат;

КонецФункции
