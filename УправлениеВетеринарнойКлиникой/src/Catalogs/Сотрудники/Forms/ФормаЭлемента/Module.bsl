#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ТекущийОбъект.Фото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФото));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ИдентификаторПользователяИБ) Тогда
		Если Не ВходРазрешен Тогда
			Возврат
		КонецЕсли;
		
		ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
	
	Иначе
		ПользовательИБ = 
			ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИдентификаторПользователяИБ);
		
		Если ПользовательИБ = Неопределено Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
		КонецЕсли;
	
	КонецЕсли;
	
	ПользовательИБ.АутентификацияСтандартная = ВходРазрешен;
	ПользовательИБ.Имя = Логин;
	ПользовательИБ.ПолноеИмя = ТекущийОбъект.Наименование;
	
	Если ПарольИзменен Тогда
		ПользовательИБ.Пароль = Пароль;
	КонецЕсли;
	
	ПользовательИБ.Роли.Очистить();
	
	Если Объект.Должность = Перечисления.Должности.Владелец Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.ПолныеПрава);
	ИначеЕсли Объект.Должность = Перечисления.Должности.Администратор Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.Администратор);
	ИначеЕсли Объект.Должность = Перечисления.Должности.Врач Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.Врач);
	ИначеЕсли Объект.Должность = Перечисления.Должности.ЗаведущийСкладом Тогда
		ПользовательИБ.Роли.Добавить(Метаданные.Роли.ЗаведущийАптекой);
	КонецЕсли;
	
	ПользовательИБ.Записать();
	
	ТекущийОбъект.ИдентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");
	
	ПарольИзменен = Ложь;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ИдентификаторПользователяИБ) Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательИБ = 
			ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ТекущийОбъект.ИдентификаторПользователяИБ);
	
	Если ПользовательИБ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Логин = ПользовательИБ.Имя;
	ВходРазрешен = ПользовательИБ.АутентификацияСтандартная;
	
	Если ПользовательИБ.ПарольУстановлен Тогда
		Пароль = Новый УникальныйИдентификатор;
	Иначе
		Пароль = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПарольИзменен = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
		АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресФотоНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ВыбратьФото();
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Очистить(Команда)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
	КонецЕсли;
		
	АдресФото = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВставитьИзБуфераОбмена(Команда)
	Если Не Ждать СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		ПредупреждениеАсинх("Невозможно вставить из буфера обмена");
	КонецЕсли;
	
	ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Картинка;
	Если Ждать СредстваБуфераОбмена.СодержитДанныеАсинх(ФорматДанных) Тогда
		
		Картинка = Ждать СредстваБуфераОбмена.ПолучитьДанныеАсинх(ФорматДанных);
		АдресФото = Ждать ПоместитьВоВременноеХранилище(Картинка, УникальныйИдентификатор);
	Иначе
		АдресФото = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура КопироватьСотрудникаВБуферОбмена(Команда)
	ДанныеСотрудника = Новый Структура;
	
	ДанныеСотрудника.Вставить("УниквльныйИдентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	ДанныеСотрудника.Вставить("Код", Объект.Код);
	ДанныеСотрудника.Вставить("Наименование", Объект.Наименование);
	ДанныеСотрудника.Вставить("Должность", Объект.Должность);
	ДанныеСотрудника.Вставить("Телефон", Объект.Телефон);
	ДанныеСотрудника.Вставить("Email", Объект.Email);
	ДанныеСотрудника.Вставить("ЧасоваяСтавка", Объект.ЧасоваяСтавка);
	
	Если ЗначениеЗаполнено(АдресФото) Тогда
		ДанныеСотрудника.Вставить("Фото", ПолучитьФотоНаСервере());
	КонецЕсли;

	
	ЭлементыБуфера = Новый Массив;
	Если СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(СтандартныйФорматДанныхБуфераОбмена.HTML) Тогда
		ЭлементыБуфера.Добавить(СотрудникКакHTML(ДанныеСотрудника));
	КонецЕсли;
	
	Если СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(СтандартныйФорматДанныхБуфераОбмена.Текст) Тогда
		ЭлементыБуфера.Добавить(СотрудникКакТекст(ДанныеСотрудника));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЭлементыБуфера) Тогда
		Возврат;
	КонецЕсли;
	
	Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ЭлементыБуфера);
	
	ПоказатьПредупреждение(, НСтр("ru = 'Данные скопированы в буфер обмена'"));
КонецПроцедуры
#КонецОбласти



#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ПолучитьФотоНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Сотрудники.Фото КАК Фото
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Фото = Выборка.Фото.Получить();
		Если Фото <> Неопределено Тогда
			Если ТипЗнч(Фото) = Тип("Картинка") Тогда
				Возврат Фото.ПолучитьДвоичныеДанные();
			Иначе
				Возврат Фото;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция СотрудникКакТекст(ДанныеСотрудника)
	
	Текст = "";
	
	Для Каждого Элемент Из ДанныеСотрудника Цикл
		Если Элемент.Ключ <> "Фото" Тогда
			Текст = Текст + Элемент.Ключ + ": " + Элемент.Значение + Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ЭлементБуфераОбмена(СтандартныйФорматДанныхБуфераОбмена.Текст, Текст);
	
КонецФункции


&НаКлиенте
Функция СотрудникКакHTML(ДанныеСотрудника)
	
	HTML = "<html><body>";
	
	Для Каждого Элемент Из ДанныеСотрудника Цикл
		Если Элемент.Ключ = "Фото" Тогда
			Если Элемент.Значение = Неопределено Тогда
				Продолжить;
			Иначе
				HTML = HTML + "<img src='data:image/jpeg;base64," + Base64Строка(Элемент.Значение) + "'/>";
			КонецЕсли;
			
		Иначе
			HTML = HTML + "<p>" + Элемент.Ключ + ": " + Элемент.Значение + "</p>";
		КонецЕсли;
	КонецЦикла;
	
	HTML = HTML + "</body></html>";
	
	Возврат Новый ЭлементБуфераОбмена(СтандартныйФорматДанныхБуфераОбмена.HTML, HTML);
	
КонецФункции

&НаКлиенте
Асинх Процедура ВыбратьФото()
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = НСтр("ru = 'Фото|*.jpg;*.png'");
		
	РезультатПомещения = Ждать ПоместитьФайлНаСерверАсинх( , , , ПараметрыДиалога, УникальныйИдентификатор);
	
	Если РезультатПомещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатПомещения.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФото = РезультатПомещения.Адрес;
	
КонецПроцедуры
#КонецОбласти