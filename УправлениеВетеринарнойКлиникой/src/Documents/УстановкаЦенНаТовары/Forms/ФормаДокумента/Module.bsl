
&НаКлиенте
Асинх Процедура Загрузить(Команда)
	
	ТекстВопроса = НСтр("ru = 'В таблице уже введены цены, при загрузке из файла таблица будет очищена. Продолжить?'");

	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = НСтр("ru = 'Файл Excel|*.xlsx'");

	РезультатПомещения = Ждать ПоместитьФайлНаСерверАсинх( , , , ПараметрыДиалога);

	Если РезультатПомещения = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если РезультатПомещения.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресФайла", РезультатПомещения.Адрес);

	Оповещение = Новый ОписаниеОповещения("ЗагрузитьЗавершение", ЭтотОбъект);

	ОткрытьФорму("Документ.УстановкаЦенНаТовары.Форма.ФормаЗагрузкиЦен", ПараметрыФормы, , , , , Оповещение);
	
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура ЗагрузитьЗавершение(Результат, ДопонительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ЗагрузитьТаблицуИзВременногоХранилища(Результат);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуИзВременногоХранилища(Адрес)

	Объект.Товары.Загрузить(ПолучитьИзВременногоХранилища(Адрес));

КонецПроцедуры
#КонецОбласти