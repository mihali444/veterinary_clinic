Процедура ОбработкаПроведения(Отказ,Режим)

	СформироватьДвижениеОстаткиТоваров();
	СформироватьДвижениеПокупка();
	СформироватьДвиженияЗадолженностьПередПоставщиками(Отказ);
	
КонецПроцедуры


Процедура СформироватьДвижениеОстаткиТоваров()

	// регистр ОстаткиТоваров
	Движения.ОстаткиТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаТовары из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Товар = ТекСтрокаТовары.Товар;
		Движение.Количество = ТекСтрокаТовары.Количество;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
	КонецЦикла;

КонецПроцедуры

Процедура СформироватьДвижениеПокупка()

	Движения.Покупка.Записывать = Истина;
	Для Каждого ТекСтрокаТовары из Товары Цикл
		Движение = Движения.Покупка.Добавить();
		Движение.Период = Дата;
		Движение.Поставщик = Поставщик;
		Движение.Товар = ТекСтрокаТовары.Товар;
		Движение.Договор = Договор;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
	КонецЦикла;

КонецПроцедуры

Процедура СформироватьДвиженияЗадолженностьПередПоставщиками(Отказ)
	
	Движения.ЗадолженностьПередПоставщиками.Записывать = Истина;
	Движения.АвансыПоставщикам.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьПередПоставщиками");
	ЭлементБлокировки.УстановитьЗначение("Поставщик", Поставщик);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыПоставщикам");
	ЭлементБлокировки.УстановитьЗначение("Поставщик", Поставщик);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
	Движения.ЗадолженностьПередПоставщиками.Записывать = Истина;
	Движения.АвансыПоставщикам.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвансыПоставщикамОстатки.СуммаОстаток КАК Аванс
		|ИЗ
		|	РегистрНакопления.АвансыПоставщикам.Остатки(
		|			&Дата,
		|			Поставщик = &Поставщик
		|				И Договор = &Договор) КАК АвансыПоставщикамОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Поставщик", Поставщик);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Аванс = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Аванс = РезультатЗапроса.Аванс;
	КонецЕсли;
	
	Если Аванс > 0 Тогда
		
		СуммаАванса = Мин(Аванс, СуммаДокумента);
		
		ДвижениеАванс = Движения.АвансыПоставщикам.Добавить();
		ДвижениеАванс.Период = Дата;
		ДвижениеАванс.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеАванс.Поставщик = Поставщик;
		ДвижениеАванс.Договор = Договор;
		ДвижениеАванс.Сумма = СуммаАванса;
		
		Если СуммаДокумента > Аванс Тогда
			Движение = Движения.ЗадолженностьПередПоставщиками.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Поставщик = Поставщик;
			Движение.Договор = Договор;
			Движение.Сумма = СуммаДокумента - Аванс;
		КонецЕсли;
		
	Иначе
		Движение = Движения.ЗадолженностьПередПоставщиками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Поставщик = Поставщик;
		Движение.Договор = Договор;
		Движение.Сумма = СуммаДокумента - Аванс;
	КонецЕсли;
	
КонецПроцедуры
