Процедура ОбработкаЗаполнения(ДанныеЗаполнения,СтандартнаяОбработка)
	//{{__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗакупкаТоваров") Тогда
		// Заполнение шапки
		Клиент = ДанныеЗаполнения.Поставщик;
		Договор = ДанныеЗаполнения.Договор;
		Сумма = ДанныеЗаполнения.СуммаДокумента;
		Ответственный = ДанныеЗаполнения.Ответственный;

	КонецЕсли;

	//}}__КОНСТРУКТОР_ВВОД_НА_ОСНОВАНИИ
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ,Режим)

	СформироватьДвиженияВзаиморасчетыСПоставщиками();
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияВзаиморасчетыСПоставщиками()

	Движения.ЗадолженностьПередПоставщиками.Записывать = Истина;
	Движения.АвансыПоставщикам.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьПередПоставщиками");
	ЭлементБлокировки.УстановитьЗначение("Поставщик", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыПоставщикам");
	ЭлементБлокировки.УстановитьЗначение("Поставщик", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Блокировка.Заблокировать();
	
	Движения.ЗадолженностьПередПоставщиками.Записывать = Истина;
	Движения.АвансыПоставщикам.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадолженностьПередПоставщикамиОстатки.СуммаОстаток КАК Долг
		|ИЗ
		|	РегистрНакопления.ЗадолженностьПередПоставщиками.Остатки(&Период, Поставщик = &Поставщик
		|	И Договор = &Договор) КАК ЗадолженностьПередПоставщикамиОстатки";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Поставщик", Клиент);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Долг = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Долг = РезультатЗапроса.Долг;
	КонецЕсли;
	
	Если Сумма <= Долг Тогда
		Движение = Движения.ЗадолженностьПередПоставщиками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Договор = Договор;
		Движение.Поставщик = Клиент;
		Движение.Сумма = Сумма;
	Иначе
		
		Если Долг > 0 Тогда
			
			Движение = Движения.ЗадолженностьПередПоставщиками.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Договор = Договор;
			Движение.Поставщик = Клиент;
			Движение.Сумма = Долг;
		КонецЕсли;
		
		ОстатокАванса = Сумма - Долг;
		
		Если ОстатокАванса > 0 Тогда
			
			Движение = Движения.АвансыПоставщикам.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Договор = Договор;
			Движение.Поставщик = Клиент;
			Движение.Сумма = ОстатокАванса;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
