#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)

	ПодготовитьСписокТоваровИУслугДляПроведения(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияЗадолженностьПокупателей(Отказ);
	СформироватьДвижениеОстаткиТоваров();
	СфромироватьДвижениеПродажи(Отказ);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Осмотр") Тогда
		// Заполнение шапки
		Осмотр = ДанныеЗаполнения.Ссылка;
		Клиент = ДанныеЗаполнения.Хозяин;
		Договор = ДанныеЗаполнения.Договор;

		Для Каждого ТекСтрокаНазначения Из ДанныеЗаполнения.Назначения Цикл
			НоваяСтрока = ПозицииТоваров.Добавить();
			НоваяСтрока.Товар = ТекСтрокаНазначения.Товар;
		КонецЦикла;

		Для Каждого ТекСтрокаОказанныеУслуги Из ДанныеЗаполнения.ОказанныеУслуги Цикл
			НоваяСтрока = ПозицииУслуг.Добавить();
			НоваяСтрока.Услуга = ТекСтрокаОказанныеУслуги.Услуга;
			НоваяСтрока.Количество = ТекСтрокаОказанныеУслуги.Количество;
		КонецЦикла;
	КонецЕсли;

	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьСписокТоваровИУслугДляПроведения(Отказ)

	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Записать();

	Блокировка = Новый БлокировкаДанных;

	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.ИсточникДанных = ПозицииТоваров;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Товар", "Товар");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

	Блокировка.Заблокировать();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЧекПозицииТоваров.Товар КАК Товар,
	|	СУММА(ЧекПозицииТоваров.Количество) КАК Количество,
	|	СУММА(ЧекПозицииТоваров.Сумма) КАК Сумма,
	|	МИНИМУМ(ЧекПозицииТоваров.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.Чек.ПозицииТоваров КАК ЧекПозицииТоваров
	|ГДЕ
	|	ЧекПозицииТоваров.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ЧекПозицииТоваров.Товар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Товар КАК Товар,
	|	ВТ_ДанныеДокумента.Количество КАК Количество,
	|	ВТ_ДанныеДокумента.Сумма КАК Сумма,
	|	ВТ_ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ОстаткиТоваровОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(&Период, Товар В
	|			(ВЫБРАТЬ
	|				ВТ_ДанныеДокумента.Товар
	|			ИЗ
	|				ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК ОстаткиТоваровОстатки
	|		ПО ВТ_ДанныеДокумента.Товар = ОстаткиТоваровОстатки.Товар";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", МоментВремени());
	
	СписокТоваровДляПроведения = НовыйСписокТоваровДляПроведения();

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл

		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ = Истина;
			ШаблонСообщения = "Не хватает %1. В наличии %2, требуется %3";
			ШаблонПоля = "ПозицииТоваров[%1].Количество";
			Сообщение = Новый СообщениеПользователю;
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Поле = СтрШаблон(ШаблонПоля, XMLСтрока(Выборка.НомерСтроки - 1));
			Сообщение.Текст = СтрШаблон(ШаблонСообщения, Выборка.Товар, Выборка.КоличествоОстаток, Выборка.Количество);
			Сообщение.Сообщить();
			Продолжить;
		КонецЕсли;

		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе
			Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
		КонецЕсли;

		СтрокаТаблицы = СписокТоваровДляПроведения.Добавить();
		СтрокаТаблицы.Товар = Выборка.Товар;
		СтрокаТаблицы.Количество = Выборка.Количество;
		СтрокаТаблицы.Сумма = Выборка.Сумма;
		СтрокаТаблицы.Себестоимость = Себестоимость;

	КонецЦикла;

	ДополнительныеСвойства.Вставить("СписокТоваровДляПроведения", СписокТоваровДляПроведения);

КонецПроцедуры

Процедура СфромироватьДвижениеПродажи(Отказ)

	Движения.Продажи.Записывать = Истина;
	Для Каждого ТекСтрокаПозицииТоваров Из ДополнительныеСвойства.СписокТоваровДляПроведения Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Клиент = Клиент;
		Движение.Товар = ТекСтрокаПозицииТоваров.Товар;
		Движение.Договор = Договор;
		Движение.Сумма = ТекСтрокаПозицииТоваров.Сумма;
		Движение.Себестоимость = ТекСтрокаПозицииТоваров.Себестоимость;
	КонецЦикла;

	Для Каждого ТекСтрокаПозицииУслуг Из ПозицииУслуг Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Клиент = Клиент;
		Движение.Услуга = ТекСтрокаПозицииУслуг.Услуга;
		Движение.Договор = Договор;
		Движение.Сумма = ТекСтрокаПозицииУслуг.Сумма;
		ЗарплатаЗаМинуту = Осмотр.Врач.ЧасоваяСтавка / 60;
		Движение.Себестоимость = ЗарплатаЗаМинуту * ТекСтрокаПозицииУслуг.Услуга.Продолжительность;
	КонецЦикла;

КонецПроцедуры

Процедура СформироватьДвижениеОстаткиТоваров()
	
	// регистр ОстаткиТоваров
	Движения.ОстаткиТоваров.Записывать = Истина;
	Для Каждого ТекСтрокаПозицииТоваров Из ДополнительныеСвойства.СписокТоваровДляПроведения Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Товар = ТекСтрокаПозицииТоваров.Товар;
		Движение.Количество = ТекСтрокаПозицииТоваров.Количество;
		Движение.Сумма = ТекСтрокаПозицииТоваров.Себестоимость;
	КонецЦикла;

КонецПроцедуры

Процедура СформироватьДвиженияЗадолженностьПокупателей(Отказ)

	Движения.ЗадолженностьПокупателей.Записывать = Истина;
	Движения.АвансыПокупателей.Записывать = Истина;
	Движения.Записать();

	Блокировка = Новый БлокировкаДанных;

	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьПокупателей");
	ЭлементБлокировки.УстановитьЗначение("Клиент", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыПокупателей");
	ЭлементБлокировки.УстановитьЗначение("Клиент", Клиент);
	ЭлементБлокировки.УстановитьЗначение("Договор", Договор);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

	Блокировка.Заблокировать();

	Движения.ЗадолженностьПокупателей.Записывать = Истина;
	Движения.АвансыПокупателей.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвансыПокупателейОстатки.СуммаОстаток КАК Аванс
	|ИЗ
	|	РегистрНакопления.АвансыПокупателей.Остатки(
	|			&Дата,
	|			Клиент = &Клиент
	|				И Договор = &Договор) КАК АвансыПокупателейОстатки";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("Клиент", Клиент);

	РезультатЗапроса = Запрос.Выполнить().Выбрать();

	Аванс = 0;

	Если РезультатЗапроса.Следующий() Тогда
		Аванс = РезультатЗапроса.Аванс;
	КонецЕсли;

	Если Аванс > 0 Тогда

		СуммаАванса = Мин(Аванс, Сумма);

		ДвижениеАванс = Движения.АвансыПокупателей.Добавить();
		ДвижениеАванс.Период = Дата;
		ДвижениеАванс.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеАванс.Клиент = Клиент;
		ДвижениеАванс.Договор = Договор;
		ДвижениеАванс.Сумма = СуммаАванса;

		Если Сумма > Аванс Тогда
			Движение = Движения.ЗадолженностьПокупателей.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Клиент = Клиент;
			Движение.Договор = Договор;
			Движение.Сумма = Сумма - Аванс;
		КонецЕсли;

	Иначе
		Движение = Движения.ЗадолженностьПокупателей.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.Сумма = Сумма;
	КонецЕсли;

КонецПроцедуры

Функция НовыйСписокТоваровДляПроведения()
	
	ТипДенежнаяСумма = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));
	ТипКоличество = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный));
	
	СписокТоваровДляПроведения = Новый ТаблицаЗначений;
	СписокТоваровДляПроведения.Колонки.Добавить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
	СписокТоваровДляПроведения.Колонки.Добавить("Сумма", ТипДенежнаяСумма);
	СписокТоваровДляПроведения.Колонки.Добавить("Количество", ТипКоличество);
	СписокТоваровДляПроведения.Колонки.Добавить("Себестоимость", ТипДенежнаяСумма);
	
	Возврат СписокТоваровДляПроведения;
	
КонецФункции

#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли